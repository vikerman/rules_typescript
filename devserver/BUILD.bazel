### START DEV

load("@build_bazel_rules_nodejs//:defs.bzl", "npm_package")
load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")

### END DEV

package(default_visibility = ["//visibility:public"])

config_setting(
    name = "darwin_amd64",
    constraint_values = [
        "@bazel_tools//platforms:osx",
        "@bazel_tools//platforms:x86_64",
    ],
)

config_setting(
    name = "linux_amd64",
    constraint_values = [
        "@bazel_tools//platforms:linux",
        "@bazel_tools//platforms:x86_64",
    ],
)

config_setting(
    name = "windows_amd64",
    constraint_values = [
        "@bazel_tools//platforms:windows",
        "@bazel_tools//platforms:x86_64",
    ],
)

filegroup(
    name = "server",
    srcs = select({
        ":darwin_amd64": ["devserver-darwin_amd64"],
        ":linux_amd64": ["devserver-linux_amd64"],
        ":windows_amd64": ["devserver-windows_amd64.exe"],
    }),
    # Don't build on CI
    tags = ["manual"],
)

### START DEV

go_library(
    name = "go_default_library",
    srcs = ["main.go"],
    importpath = "github.com/bazelbuild/rules_typescript/devserver",
    visibility = ["//visibility:private"],
    deps = [
        "//devserver/concatjs:go_default_library",
        "//devserver/devserver:go_default_library",
    ],
)

go_binary(
    name = "devserver",
    embed = [":go_default_library"],
    visibility = ["//visibility:public"],
)

go_binary(
    name = "devserver-windows",
    out = "devserver-windows_amd64.exe",
    embed = [":go_default_library"],
    goarch = "amd64",
    goos = "windows",
    pure = "on",
    visibility = ["//visibility:public"],
)

go_binary(
    name = "devserver-darwin",
    out = "devserver-darwin_amd64",
    embed = [":go_default_library"],
    goarch = "amd64",
    goos = "darwin",
    pure = "on",
    visibility = ["//visibility:public"],
)

go_binary(
    name = "devserver-linux",
    out = "devserver-linux_amd64",
    embed = [":go_default_library"],
    goarch = "amd64",
    goos = "linux",
    pure = "on",
    visibility = ["//visibility:public"],
)

npm_package(
    name = "package",
    srcs = [
        "BUILD.bazel",
    ],
    replacements = {
        "###\\s*START\\s*DEV\\s*[\s\S]*###\\s*END\\s*DEV": "",
    },
    deps = [
        "//devserver:devserver-darwin",
        "//devserver:devserver-linux",
        "//devserver:devserver-windows",
    ],
)

### END DEV
